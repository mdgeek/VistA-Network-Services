RGNETBAS ;RI/CBMI/DKM - Asynchronous RPC calls ;04-Apr-2015 08:42;DKM
 ;;1.0;NETWORK SERVICES;;01-Apr-2015
 ;=================================================================
 ; Background task for executing an asynchronous RPC
TASK N RGD,TGT,$ET,$ES
 S $ET="D ERROR^RGNETBAS",ZTREQ="@"
 K ^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK)
 Q:$$S^%ZTLOAD
 S ^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK)=0,TGT=$NA(^(ZTSK,0))
 S:XWBPTYPE=4 RGD=TGT
 D DORPC^RGNETBAC
 I XWBPTYPE=1 S @TGT@(0)=RGD
 E  I XWBPTYPE>1,XWBPTYPE<4 M @TGT=RGD S:XWBPTYPE=2 XWBWRAP=1
 E  I XWBPTYPE=4,$L(RGD),RGD'=TGT M @TGT=@RGD K @RGD
 E  I XWBPTYPE=5 S @TGT@(0)=$G(@RGD)
 E  I XWBPTYPE="H" D
 .N X,Y,FIL,DIR
 .S X=$E($$DIRDLM^RGUTOS,3),Y=$L(RGD,X),DIR=$P(RGD,X,1,Y-1),FIL=$P(RGD,X,Y)
 .I '$$FTG^%ZISH(DIR,FIL,$NA(@TGT@(0)),$QL(TGT)+1)
 .D DELETE^RGUTOS(RGD)
 I $$S^%ZTLOAD K ^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK)
 E  S ^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK)=ZTSK_"^0^"_XWBWRAP
 Q
ERROR D SETERR(-1,$$EC^%ZOSV)
 D ^%ZTER,UNWIND^%ZTER
 Q
 ; Retrieve specified routine line
RTNTEXT(RTN) ;
 N TAG
 S TAG=$P(RTN,U),RTN=$P(RTN,U,2)
 Q $S('$L(RTN):$T(^@TAG),$L(TAG):$T(@TAG^@RTN),1:$T(^@RTN))
 ; Set error info to return to client
SETERR(CODE,TEXT) ;
 K ^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK) M ^(ZTSK,0,0)=TEXT
 S ^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK)=ZTSK_U_CODE_"^1"
 Q
 ; RPC to check for completed tasks
 ; Results[0] at the client end will contain the TaskMan ID of the
 ; completed RPC. The remainder of the Result will be the data
 ; returned by the RPC.
ASYCHK() N ZTSK,RGD,X
 S ZTSK=0
 F  S ZTSK=+$O(^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK)) Q:'ZTSK  S X=$G(^(ZTSK)),RGD=$NA(^(ZTSK,0)) I X D  Q
 .D TCPWRITE^RGNETBRK($C(2)_X_$C(13))
 .D ARYOUT^RGNETBRK(RGD,$P(X,U,3))
 .K ^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK)
 Q ZTSK
 ; RPC: Stop a task
 ; Signals to a backround task that it should stop running.  Cleans up
 ; any output generated by completed tasks.
STOP(RGD,ZTSK) ;
 S RGD=''$D(^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK)) K:RGD ^(ZTSK)
 Q:'$D(^%ZTSK(ZTSK))
 S $P(^%ZTSK(ZTSK,.1),U,10)=$P($G(^VA(200,DUZ,0)),U)
 D DQ^%ZTLOAD
 I $G(ZTSK(0)) D KILL^%ZTLOAD
 Q
 ; Stop all tasks in task list
STOPALL N ZTSK
 S ZTSK=0
 F  S ZTSK=$O(^XTMP("RGNETB",RGNETB("UID"),"T",ZTSK)) Q:'ZTSK  D STOP(,ZTSK)
 K ^XTMP("RGNETB",RGNETB("UID"),"T")
 Q
